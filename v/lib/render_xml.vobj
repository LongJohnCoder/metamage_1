const mapping = ... => ...

def qq { str ('"', _, '"') }

def needs_escape
{
	return _ in [ '<', '&', '"', '\'' ];
}

def escaped
{
	const c (byte)  = _
	
	const code = str int c
	
	return "&#" code ";"
}

def escape
{
	const text = _
	
	const echars = text map { if needs_escape _ then {escaped _} else {_} }
	
	return str echars
}

def render_xml_at_depth
{
	const printer, const node, const depth = _
	
	const print = printer % depth
	
	if node isa string then
	{
		print escape node
		return ()
	}
	
	if node isa mapping then
	{
		const tag   = node
		const name  = tag.key
		const attrs = tag.value
		
		const attr = str( attrs map { " " _.key, "=", qq _.value } )
		
		print "<" name attr "/>"
		
		return ()
	}
	
	if node isa array then
	{
		if node[ 0 ] isa mapping then
		{
			const tag   = node[ 0 ]
			const name  = tag.key
			const attrs = tag.value
			
			const attr = str( attrs map { " " _.key, "=", qq _.value } )
			
			if node.length == 2 and node[ 1 ] isa string then
			{
				const inner = escape node[ 1 ]
				
				print "<" name attr ">" inner "</" name ">"
			}
			else
			{
				print "<" name attr ">"
				render_xml_at_depth( printer, node[ 1 -> node.length ], depth )
				print "</" name ">"
			}
		}
		else
		{
			for subnode in node do
			{
				render_xml_at_depth( printer, subnode, depth + 1 )
			}
		}
		return ()
	}
	
	throw "invalid XML node"
}

export
def render_xml
{
	const printer, const doc = _
	
	render_xml_at_depth( printer, doc, 0 )
}
