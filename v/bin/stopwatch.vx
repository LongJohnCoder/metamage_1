#!/usr/bin/env vx

var start = ()
var timer

OUT <== "-:--"

const events = channel()

var reader = thread lambda
{
	while var input = IN.read 1 do
	{
		const c = *input
		
		if c == ' ' then
		{
			events <== c
		}
	}
}

def tens
{
	return ("0" * (_ < 10)) _
}

for ev in events do
{
	if ev == '\0' then
	{
		const dt = time() - start
		const m  = dt div 60
		const ss = dt mod 60
		
		OUT <== "\r" m ":" tens ss
	}
	else if ev == ' ' then
	{
		if (+) start then
		{
			OUT <== "\n"
			
			exit 0
		}
		
		start = time()
		
		timer = thread { while true do { events <== '\0'; sleep 1 } }
	}
}
