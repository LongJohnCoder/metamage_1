#!/usr/bin/perl

# Our custom MacRelix runtime has no perl library
#use warnings;
#use strict;


sub spew
{
	my ( $path, $data ) = @_;
	
	open my $out, ">", $path or die "$path: $!\n";
	
	print $out $data or die "$path: $!\n";
	
	close $out or die "$path: $!\n";
}

sub set_button_title
{
	my ( $title ) = @_;
	
	spew( "v/button/v/v/title", "$title\n" );
}

sub set_prompt_text
{
	my ( $text ) = @_;
	
	spew( "v/prompt/v/v/text", $text );
}

sub set_checksum_text
{
	my ( $text ) = @_;
	
	spew( "v/checksum/v/v/text", $text );
}

sub wait_for_button
{
	open my $in, "<", "v/button/v/v/socket" or die;
	
	sysread $in, my( $buffer ), 1 or die;
}

sub rom_checksum
{
	my ( $path ) = @_;
	
	my $output = `/usr/bin/rom-checksum $path`;
	
	chop $output;
	
	return $output;
}

sub md5sum
{
	my ( $path ) = @_;
	
	my $output = `/usr/bin/md5sum $path`;
	
	return substr( $output, 0, 32 );
}

my $progress = q|perl -pe 'm{^ \s* (\d+) \s* / \s* (\d+) \s* $}x and $_ = ($1/$2 * 10000) . "\n"'|;

sub copy_with_progress
{
	my ( $src, $dest ) = @_;
	
	link "/gui/new/progress", "v/progress/v/view";
	
	system "/usr/bin/copy --dump-progress $src $dest | $progress > v/progress/v/v/value";
	system "/bin/sync";
	
	unlink "v/progress/v/view";
}

sub rom_copier
{
	my $checksum = rom_checksum( "/sys/mac/rom" );
	
	set_checksum_text( $checksum );
	
	if ( my $valid = $checksum =~ m{^ [0-9A-Fa-f]{8} $}x )
	{
		set_prompt_text( "ROM + MD5 checksum:" );
		
		set_checksum_text( "$checksum + (calculating MD5...)");
		
		my $md5sum = md5sum( "/sys/mac/rom" );
		
		set_checksum_text( "$checksum + $md5sum");
		
		my $md5sum_12 = substr( $md5sum, 0, 12 );
		
		my $file = "/$checksum-$md5sum_12.rom";  # 8 + 1 + 12 + 4 = 25
		
		set_button_title( "Copy" );
		
		if ( -e $file )
		{
			set_prompt_text( "The file $file already exists." );
			
			return;
		}
		
		wait_for_button();
		
		if ( -e $file )
		{
			set_prompt_text( "The file $file DIDN'T exist, but now does." );
			
			return;
		}
		
		set_button_title( "Wait" );
		
		set_prompt_text( "Copying..." );
		
		copy_with_progress( "/sys/mac/rom", $file );
		
		set_prompt_text( "Verifying ROM checksum..." );
		
		my $new_checksum = rom_checksum( $file );
		
		if ( $new_checksum ne $checksum )
		{
			set_prompt_text( "The copy's ROM checksum DOES NOT MATCH" );
			
			return;
		}
		
		set_prompt_text( "Verifying MD5 checksum..." );
		
		my $new_md5sum = md5sum( $file );
		
		if ( $new_md5sum ne $md5sum )
		{
			set_prompt_text( "The copy's MD5 checksum DOES NOT MATCH" );
			
			return;
		}
		
		set_prompt_text( "Copy and verification succeeded." );
	}
}

if ( -f "/sys/mac/rom" )
{
	rom_copier();
}
else
{
	set_checksum_text( "ROM not found" );
}

set_button_title( "Quit" );

wait_for_button();

system( "/sys/kernel/bin/quit" );
